%{
import java.util.Map;
import java.util.List;
import org.jeesonmap.JeesonMap;
import org.jeesonmap.JeesonArray;
%}

%class JeesonParser
%interface JeesonParserTokens
%package org.jeesonmap

%semantic Object
%token OP_BRK CL_BRK STR NUM COM DD OP_SQR CL_SQR BOOL NULL

%%

start:
   json {
       $$ = $1;
       this.parsedResult = $$;
   }
   ;

json:
   object {
       $$ = $1;
   }
   | array {
       $$ = $1;
   }
   ;

object:
   OP_BRK pair_list CL_BRK {
       $$ = $2; 
   }
   | OP_BRK CL_BRK {
       $$ = new JeesonMap(); 
   }
   ;

array:
   OP_SQR value_list CL_SQR {
       $$ = $2; 
   }
   | OP_SQR CL_SQR {
       $$ = new JeesonArray(); 
   }
   ;

value:
   STR {
       $$ = (String) $1; 
   }
   | NUM {
       $$ = Integer.parseInt((String) $1); 
   }
   | array {
       $$ = $1; 
   }
   | object {
       $$ = $1; 
   }
   | BOOL {
       $$ = Boolean.parseBoolean((String) $1); 
   }
   | NULL {
       $$ = null; 
   }
   ;

value_list:
   value {
       JeesonArray list = new JeesonArray();
       list.add($1);
       $$ = list;
   }
   | value_list COM value {
       ((JeesonArray) $1).add($3);
       $$ = $1;
   }
   | value_list COM {
       yyerror("There is an extra comma in the array at line " + lexer.getLine() + ", column " + lexer.getColumn() + "!!! Remove the comma or add another value.");
   }
   ;

key:
   STR {
       $$ = (String) $1;
   }
   ;

pair:
   key DD value {
       JeesonMap pairMap = new JeesonMap();
       pairMap.put((String) $1, $3);
       $$ = pairMap;
   }
   | key value {
       yyerror("There is a missing colon (:) at line " + lexer.getLine() + ", column " + lexer.getColumn() + "!!! Maybe you forgot it while adding a new key/value pair.");
   }
   ;

pair_list:
   pair {
       $$ = $1;
   }
   | pair_list COM pair {
       ((JeesonMap) $1).putAll((JeesonMap) $3);
       $$ = $1;
   }
   | pair_list COM {
       yyerror("There is an extra comma in the object at line " + lexer.getLine() + ", column " + lexer.getColumn() + "!!! Remove the comma or add another key/value pair.");
   }
   ;
%%

private JeesonLexer lexer;
private Object parsedResult;

public JeesonParser(JeesonLexer lexer) {
    this.lexer = lexer;
}

public void yyerror(String error) {
    throw new RuntimeException("Parsing stopped because of this error: " + error);
}

public Object getParsedResult() {
    return this.parsedResult;
}
